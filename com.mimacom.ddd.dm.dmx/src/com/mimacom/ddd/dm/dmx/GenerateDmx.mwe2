module com.mimacom.ddd.dm.dmx.GenerateDmx

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."
var project="com.mimacom.ddd.dm.dmx"
var genModel="platform:/resource/${project}/model/dmx.genmodel"
//var baseGenModel="platform:/resource/com.mimacom.ddd.dm.dmx/model/base.genmodel"

Workflow {
	
	// Generate EMF Models
	bean = org.eclipse.emf.mwe.utils.StandaloneSetup {
		scanClassPath = true
	}
	component = org.eclipse.emf.mwe.utils.DirectoryCleaner {
		directory = "${rootPath}/${project}/emf-gen"
	}
	component = org.eclipse.emf.mwe2.ecore.EcoreGenerator {
		genModel = "${genModel}"
		srcPath = "platform:/resource/${project}/src"
	}
	bean =  org.eclipse.emf.mwe.utils.StandaloneSetup {
	    platformUri = "${rootPath}"
	    scanClassPath = true
	    registerGeneratedEPackage = "com.mimacom.ddd.dm.base.BasePackage"
	    registerGenModelFile = "${genModel}"
	}
	bean =  org.eclipse.emf.mwe.utils.StandaloneSetup {
	    platformUri = "${rootPath}"
	    scanClassPath = true
	    registerGeneratedEPackage = "${project}.DmxPackage"
	    registerGenModelFile = "${genModel}"
	}
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "${project}"
				rootPath = rootPath
				runtimeTest = {
					enabled = true
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}
		language = StandardLanguage {
			name = "${project}.Dmx"
			fileExtensions = "dmx"
			//referencedResource =  "${baseGenModel}"
			referencedResource =  "${genModel}"

			serializer = {
				generateStub = false
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
				// Generates checks for @Deprecated grammar annotations, an IssueProvider and a corresponding PropertyPage
				generateDeprecationValidation = true
			}
			junitSupport = {
				junitVersion = "5"
			}
		}
	}
}
