/*
 * generated by Xtext 2.16.0
 */
package com.mimacom.ddd.sm.sms.formatting2

import com.google.inject.Inject
import com.mimacom.ddd.sm.sms.SAggregate
import com.mimacom.ddd.sm.sms.SComplexType
import com.mimacom.ddd.sm.sms.SDomain
import com.mimacom.ddd.sm.sms.SEnumeration
import com.mimacom.ddd.sm.sms.SImport
import com.mimacom.ddd.sm.sms.SMorphRule
import com.mimacom.ddd.sm.sms.SType
import com.mimacom.ddd.sm.sms.services.SmsGrammarAccess
import org.eclipse.xtext.formatting2.AbstractFormatter2
import org.eclipse.xtext.formatting2.IFormattableDocument

class SmsFormatter extends AbstractFormatter2 {
	
	@Inject extension SmsGrammarAccess

	def dispatch void format(SDomain domain, extension IFormattableDocument document) {
		// TODO: format HiddenRegions around keywords, attributes, cross references, etc. 
		domain.regionFor.assignment(SDomainAccess.nameAssignment_1).append[newLines = 2]
		for (i : domain.imports) {
			i.append[newLine]
		}
		domain.imports.last.append[newLines = 2]
		
		for (type : domain.types) {
			type.format
		}
		for (aggregate : domain.aggregates) {
			aggregate.format
		}
	}
	
	def dispatch void format(SImport imp, extension IFormattableDocument document) {
		imp.append[newLine]
	}

	def dispatch void format(SMorphRule sMorphRule, extension IFormattableDocument document) {
		// TODO: format HiddenRegions around keywords, attributes, cross references, etc. 
		sMorphRule.remultiplyTo.format
	}
	
	def dispatch void format(SAggregate aggregate, extension IFormattableDocument document) {
		val open = aggregate.regionFor.keyword(SAggregateAccess.leftCurlyBracketKeyword_1_1_1)
		val close = aggregate.regionFor.keyword(SAggregateAccess.rightCurlyBracketKeyword_1_1_3)
		open.append[newLines = 2]
		interior(open, close) [indent]
		
		for (type : aggregate.types) {
			type.format
		}
	}
	
	def dispatch void format(SEnumeration  en, extension IFormattableDocument document) {
		if (en.literals.size > 3) {
			val open = en.regionFor.keyword(SEnumerationAccess.leftCurlyBracketKeyword_1)
			val close = en.regionFor.keyword(SEnumerationAccess.rightCurlyBracketKeyword_4)
			open.append[newLine]
			interior(open, close) [indent]
			
			for( literal : en.literals) {
				literal.regionFor.assignment(SLiteralAccess.nameAssignment_2).surround[noSpace]
			}
			for( comma : en.regionFor.keywords(SEnumerationAccess.commaKeyword_2_1_0)) {
				comma.append[newLine]
			}
			en.literals.last.append[newLine]
		}
		en.append[newLines = 2]
	}
	
	def dispatch void format(SComplexType type, extension IFormattableDocument document) {
		val open = type.regionFor.keyword(SComplexTypeFeaturesAccess.leftCurlyBracketKeyword_0)
		val close = type.regionFor.keyword(SComplexTypeFeaturesAccess.rightCurlyBracketKeyword_2)
		open.append[newLine]
		interior(open, close) [indent]
		close.append[newLines = 2]
		
		for (feature: type.features) {
			feature.append[newLine]
		}
		
		for (constraint: type.constraints) {
			constraint.append[newLine]
		}
	}
	
	def dispatch void format(SType type, extension IFormattableDocument document) {
		type.append[newLines = 2]
	}
}
