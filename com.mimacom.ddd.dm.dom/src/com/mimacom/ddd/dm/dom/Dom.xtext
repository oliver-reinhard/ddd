grammar com.mimacom.ddd.dm.dom.Dom with  com.mimacom.ddd.dm.dmx.Dmx

import "http://www.mimacom.com/ddd/dm/dom"
import "http://www.mimacom.com/ddd/dm/dmx"
import "http://www.mimacom.com/ddd/dm/base" 

DomModel:
	(imports+=DImport)*
	'domain'
	name=DQualifiedName
	(snapshots+=DomSnapshot)+;

DomSnapshot:
	'snapshot' name=ID
	'{'
	(objects+=DomObject)*
	'}';

DomObject:
	DomNamedComplexObject;
	
DomNamedComplexObject:
	name=ID
	'=' 
	object=(DomEntity | DomDetail);

DomEntity:
	'entity' 
	DmxComplexObject;

DomDetail:
	'detail' 
	DmxComplexObject;

fragment DmxComplexObject:
	type=[DComplexType|ID]
	DomFieldListStartSymbol
	(fields+=DmxField)*
	'}';
	
DmxField:
	feature=[DFeature|ID] "=" value=DExpression;
	
@Override 
DmxLiteralExpression returns DExpression:
	DmxBooleanLiteral |
	DmxStringLiteral |
	DmxNaturalLiteral |
	DmxDecimalLiteral |
	DmxUndefinedLiteral |
	DomDetail;

DomFieldListStartSymbol:
	'{';
