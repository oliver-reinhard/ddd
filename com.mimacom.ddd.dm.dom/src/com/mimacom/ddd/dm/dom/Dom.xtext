grammar com.mimacom.ddd.dm.dom.Dom with  com.mimacom.ddd.dm.dmx.Dmx

import "http://www.mimacom.com/ddd/dm/dom"
import "http://www.mimacom.com/ddd/dm/base" 

DomModel:
	(imports+=DImport)*
	'domain'
	name=DQualifiedName
	(snapshots+=DomSnapshot)+;

DomSnapshot:
	'snapshot' name=ID
	'{'
	(objects+=DomObject)*
	'}';

DomObject:
	DomNamedComplexObject;
	
DomNamedComplexObject:
	name=ID
	'=' 
	object=(DomEntity | DomDetail);

DomEntity:
	'entity' 
	DomComplexObject;

DomDetail:
	'detail' 
	DomComplexObject;

fragment DomComplexObject:
	ref=[DDetailType|ID]
	DomFieldListStartSymbol
	(fields+=DomField)*
	'}';
	
DomField:
	ref=[DFeature|ID] "=" value=DomFieldValue;
	
DomFieldValue returns DExpression:
	DmxOrExpression | DomDetail;

DomFieldListStartSymbol:
	'{';
