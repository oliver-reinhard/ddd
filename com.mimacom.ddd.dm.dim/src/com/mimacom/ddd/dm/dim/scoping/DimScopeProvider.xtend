/*
 * generated by Xtext 2.16.0
 */
package com.mimacom.ddd.dm.dim.scoping

import com.google.common.collect.Lists
import com.mimacom.ddd.dm.base.BasePackage
import com.mimacom.ddd.dm.base.DAggregate
import com.mimacom.ddd.dm.base.DAssociation
import com.mimacom.ddd.dm.base.DAttribute
import com.mimacom.ddd.dm.base.DDetailType
import com.mimacom.ddd.dm.base.DDomain
import com.mimacom.ddd.dm.base.DIdentityType
import com.mimacom.ddd.dm.base.DQuery
import com.mimacom.ddd.dm.base.DQueryParameter
import com.mimacom.ddd.dm.base.DRelationship
import com.mimacom.ddd.dm.base.DRootType
import com.mimacom.ddd.dm.base.DTypedMember
import org.eclipse.emf.ecore.EObject
import org.eclipse.emf.ecore.EReference
import org.eclipse.xtext.EcoreUtil2
import org.eclipse.xtext.scoping.IScope
import org.eclipse.xtext.scoping.Scopes

/**
 * This class contains custom scoping description.
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#scoping
 * on how and when to use it.
 */
class DimScopeProvider extends AbstractDimScopeProvider {

	val epackage = BasePackage.eINSTANCE

	override getScope(EObject context, EReference reference) {
		
		if (reference == epackage.DTypedMember_Type) {
			
			 val IScope scope = switch context {	
			 	DAttribute: if (context.detail == true) getDefaultScopeForType(context, epackage.DDetailType) else getDefaultScopeForType(context,  epackage.IValueType)
				DQuery: getLocalRootTypeScope(context, getDefaultScopeForType(context, epackage.IValueType))
				DAssociation: getDefaultScopeForType(context, epackage.DRootType)
				DQueryParameter: getLocalRootTypeScope(context, getDefaultScopeForType(context,  epackage.IValueType))
				default:  getDefaultScopeForType(context, epackage.IValueType)
			}
			return scope
			
		} else if (reference == epackage.DComplexType_SuperType) {
			
			return switch context {
				DRootType:  getIdentityTypeScope(context, DRootType)
				DRelationship: getIdentityTypeScope(context, DRelationship)
				DDetailType: getDefaultScopeForType(context, epackage.DDetailType)
				default:  IScope.NULLSCOPE
			}
		} 
		return super.getScope(context, reference)
	}
		
	def IScope getLocalRootTypeScope(DTypedMember context, IScope outerScope) {
		val aggregate = EcoreUtil2.getContainerOfType(context, DAggregate)
		if (aggregate?.root !== null) {
			return Scopes.scopeFor(Lists.newArrayList(aggregate.root), outerScope)	
		}
		return outerScope
	}
		
	def IScope getIdentityTypeScope(DIdentityType context, Class<?> type) {
		val domain = EcoreUtil2.getContainerOfType(context, DDomain)
		if (domain !==  null) {
			val list = Lists.newArrayList
			for (a:domain.aggregates) {
				val root = a.root
				if (root !== null && root !== context && type.isAssignableFrom(root.class) ) {
					list.add(root)
				}
			}
			return Scopes.scopeFor(list)
		}
		return IScope.NULLSCOPE
	}
}
