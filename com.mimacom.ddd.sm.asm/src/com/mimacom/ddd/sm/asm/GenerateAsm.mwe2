module com.mimacom.ddd.sm.asm.GenerateAsm

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."
var project="com.mimacom.ddd.sm.asm"
var genModel="platform:/resource/${project}/model/asm.genmodel"

Workflow {
	
	// Generate EMF Models
	bean = org.eclipse.emf.mwe.utils.StandaloneSetup {
		scanClassPath = true
	}
	component = org.eclipse.emf.mwe.utils.DirectoryCleaner {
		directory = "${rootPath}/${project}/emf-gen"
	}
	component = org.eclipse.emf.mwe2.ecore.EcoreGenerator {
		genModel = "${genModel}"
		srcPath = "platform:/resource/${project}/src"
	}
	bean =  org.eclipse.emf.mwe.utils.StandaloneSetup {
	    platformUri = "${rootPath}"
	    scanClassPath = true
	    registerGeneratedEPackage = "${project}.AsmPackage"
	    registerGenModelFile = "${genModel}"
	}
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "${project}"
				rootPath = rootPath
				runtimeTest = {
					enabled = true
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}
		language = StandardLanguage {
			name = "${project}.Asm"
			fileExtensions = "asm"
//			referencedResource = "platform:/resource/com.mimacom.ddd.dm.base/model/base.genmodel"
			referencedResource = "platform:/resource/com.mimacom.ddd.sm.sim/model/sim.genmodel"
			referencedResource = "platform:/resource/${project}/model/asm.genmodel"

			serializer = {
				generateStub = false
			}
			validator = {
				composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
				// Generates checks for @Deprecated grammar annotations, an IssueProvider and a corresponding PropertyPage
				generateDeprecationValidation = true
			}
			fileWizard = {
				generate = true
			}
			junitSupport = {
				junitVersion = "5"
			}
		}
	}
}
