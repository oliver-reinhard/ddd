grammar com.mimacom.ddd.sm.asm.Asm with com.mimacom.ddd.dm.dmx.Dmx

import "http://www.mimacom.com/ddd/dm/base"
import "http://www.mimacom.com/ddd/dm/dmx" 
import "http://www.mimacom.com/ddd/sm/sim"
//import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.mimacom.com/ddd/sm/asm"

@Override 
DNamespace:
	'namespace'
	name=DQualifiedName
	(imports+=DImport)*
	model=(SApplication | SServiceInterface);


SApplication:
	'application'
	name=ID
	'information' 'model' model=[SInformationModel|DQualifiedName]
	(actors+=SActor)*;

SServiceInterface:
	'service' 'interface'
	name=ID
	'interface' 'model' interface=[SInformationModel|DQualifiedName]
	'core' 'model' core=[SInformationModel|DQualifiedName]
	(exceptions+=SException)*
	(operations+=SServiceOperation)*;

SActor:
	SWatchdog | SHuman;

SWatchdog:
	'watchdog'
	name=ID;

SHuman:
	'human'
	name=ID;

SServiceOperation:
	'operation'
	name=ID
	'(' 	(parameters+=SServiceParameter ( "," parameters+=SServiceParameter)*)?
	')'
	('raises' raises+=[SException|ID]  ( "," raises+=[SException|ID])* )?
	'{'
	('guard' guards+=DExpression)*
	('effect' effects+=DExpression)*
	'}';

SServiceParameter:
	direction=SDirection
	name=ID
	':'
	type=[DType|ID]  (multiplicity=DMultiplicity)?
	(description=DRichText)?;

SException:
	'exception'
	name=ID
	(description=DRichText)?;
	
enum SServiceKind:
				SYNCHRONOUS = 'synchronous' | ASYNCHRONOUS = 'async';

enum SDirection:
				INBOUND = 'in' | OUTBOUND = 'out';