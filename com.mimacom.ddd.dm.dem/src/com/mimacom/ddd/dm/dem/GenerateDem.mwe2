module com.mimacom.ddd.dm.dem.GenerateDem

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."
var project="com.mimacom.ddd.dm.dem"
var genModel="platform:/resource/${project}/model/dem.genmodel"
var baseGenModel="platform:/resource/com.mimacom.ddd.dm.base/model/base.genmodel"
var dmxGenModel="platform:/resource/com.mimacom.ddd.dm.dmx/model/dmx.genmodel"

Workflow {
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "${project}"
				rootPath = rootPath
				runtimeTest = {
					enabled = true
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}
		language = StandardLanguage {
			name = "${project}.Dem"
			fileExtensions = "dem"
			referencedResource =  "${baseGenModel}"
			referencedResource =  "${dmxGenModel}"
			
			fragment = ecore2xtext.Ecore2XtextValueConverterServiceFragment2 auto-inject {}

			formatter = {
				generateStub = true
			}

			serializer = {
				generateStub = false
			}
			validator = {
				composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
				// Generates checks for @Deprecated grammar annotations, an IssueProvider and a corresponding PropertyPage
				generateDeprecationValidation = true
			}
			fileWizard = {
				generate = true
			}
			junitSupport = {
				junitVersion = "5"
			}
		}
	}
}
