@Ecore(nsURI="http://www.mimacom.com/ddd/pub/proto")
@GenModel(modelDirectory="/com.mimacom.ddd.pub.proto/emf-gen")

package com.mimacom.ddd.pub.proto

import com.mimacom.ddd.dm.base.DExpression
import com.mimacom.ddd.dm.base.DRichText

class PublicationClass {
	String name  // id
	PublicationNature nature
	String laTeXClass
	contains ProtoSymbol[] symbols
	contains DRichText laTeXPreamble
	contains DRichText htmlPreamble
	contains ProtoDocumentSegment[] segments opposite parent
	contains ProtoDivision[] divisions
}

enum PublicationNature { Print, Web }

class ProtoSymbol {
	String name
}

class ProtoSymbolReference extends DExpression {
	refers ProtoSymbol target
}

abstract class ProtoDocumentSegment {
	derived String name get {
		eClass.name.toLowerCase
	}
	String title
	boolean optional = "false"
	container PublicationClass parent opposite segments
}

class ProtoAbstract extends ProtoDocumentSegment { }

class ProtoPreface extends ProtoDocumentSegment { }

class ProtoEpilogue extends ProtoDocumentSegment { }

class ProtoPublicationBody extends ProtoDocumentSegment {
	boolean allowParts = "false"
	boolean allowAppendix = "false"
}

class ProtoChangeHistory extends ProtoDocumentSegment { }

class ProtoTOC extends ProtoDocumentSegment {  
	int maxLevel = "3"
}

class ProtoAbbreviations extends ProtoDocumentSegment { }

class ProtoListOfTables extends ProtoDocumentSegment { }

class ProtoListOfFigures extends ProtoDocumentSegment { }

class ProtoBibliography extends ProtoDocumentSegment { }

class ProtoGlossary extends ProtoDocumentSegment { }

class ProtoIndex extends ProtoDocumentSegment { }

// 
// Divisions
//

abstract class ProtoDivision { 
	derived String name get {
		eClass.name.toLowerCase
	}
	ProtoSequenceNumberStyle numberingStyle
}

enum ProtoSequenceNumberStyle { Arabic, CapitalRoman, SmallRoman, CapitalLetter, SmallLetter, None }

class ProtoPart extends ProtoDivision { }

class ProtoAppendix extends ProtoPart { }

class ProtoChapter extends ProtoDivision { }

class ProtoSection extends ProtoDivision { }

class ProtoSubsection extends ProtoDivision { }

class ProtoSubsubsection extends ProtoDivision { }

